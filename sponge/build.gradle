import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id 'org.spongepowered.gradle.plugin' version '2.0.2'
    id 'net.kyori.blossom' version '1.2.0'
}

repositories {
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
}

blossom {
    replaceToken("@pluginVersion@", project.ext['pluginVersion'])
}

sponge {
    apiVersion("8.1.0")
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }

    license("GNU General Public License v3.0")

    plugin(pluginId) {
        displayName("AurionChat")
        entrypoint("com.mineaurion.aurionchat.sponge.AurionChat")
        description("Chat across server with rabbitmq")
        links {
            homepage("https://mineaurion.com/")
            source("https://github.com/Mineaurion/AurionChat")
            issues("https://github.com/Mineaurion/AurionChat/issues")
        }
        contributor("Yann151924") {
            description("Lead Developer")
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("luckperms") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
            version("5.4.25")
        }
    }
}

dependencies {
    implementation project(':common')
    compileOnly 'org.spongepowered:spongeapi:8.1.0'
}


shadowJar {
    relocate 'com.rabbitmq', 'com.mineaurion.aurionchat.lib.rabbitmq'
    relocate 'org.slf4j', 'com.mineaurion.aurionchat.lib.slf4j'
    relocate 'net.kyori.adventure', 'com.mineaurion.aurionchat.lib.adventure'
    relocate 'net.kyori.event', 'com.mineaurion.aurionchat.lib.eventbus'
}